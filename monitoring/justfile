name := "monitoring"
packages := "
bwm-ng
igt-gpu-tools
iotop
"
base_images := "
quay.io/fedora/fedora-coreos:stable
quay.io/fedora-ostree-desktops/silverblue:41
quay.io/fedora-ostree-desktops/kinoite:41
"

import '../sysext.just'

all: default

# Custom download step to get bandwhich
download-manual:
    #!/bin/bash
    set -euo pipefail
    # set -x

    mkdir binaries
    cd binaries

    echo "⬇️ Downloading bandwhich"
    destfile="bandwhich-v0.23.0-x86_64-unknown-linux-gnu.tar.gz"
    wget "https://github.com/imsnif/bandwhich/releases/download/v0.23.0/${destfile}"
    hash="be4ed3a35efa76e78f6ab92c708c9672145b4129f459f6950e48c0458cf727d7  ${destfile}"
    echo "${hash}" | sha256sum -c -
    tar xf "${destfile}"
    gzip -9 assets/bandwhich.1

# Custom installation step to install bandwhich
install-manual:
    #!/bin/bash
    set -euo pipefail
    # set -x

    if [[ "${UID}" == "0" ]]; then
        SUDO=""
    else
        SUDO="sudo"
    fi

    cd rootfs

    echo "➡️ Installing bandwhich"
    ${SUDO} install -D -m 755 ../binaries/bandwhich             usr/bin/bandwhich
    ${SUDO} install -D -m 644 ../binaries/assets/bandwhich.bash usr/share/bash-completion/completions/bandwhich.bash
    ${SUDO} install -D -m 644 ../binaries/assets/_bandwhich     usr/share/zsh/site-functions/_bandwhich
    ${SUDO} install -D -m 644 ../binaries/assets/bandwhich.fish usr/share/fish/vendor_completions.d/bandwhich.fish
    ${SUDO} install -D -m 644 ../binaries/assets/bandwhich.1.gz usr/share/man/man1/bandwhich.1.gz

    ${SUDO} chown -R root: usr
