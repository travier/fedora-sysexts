name := "bitwarden"
packages := "bitwarden"
pre_commands := "rm -rf /opt; mkdir /opt"
base_images := "
quay.io/fedora-ostree-desktops/base-atomic:41 x86_64
quay.io/fedora-ostree-desktops/base-atomic:42 x86_64
"

import '../sysext.just'

all: default

download-rpms target arch=arch:
    #!/bin/bash
    set -euo pipefail
    if [[ -n "{{debug}}" ]]; then
      set -x
    fi

    mkdir -p rpms
    curl --location --fail --output rpms/bitwarden.rpm \
        "https://bitwarden.com/download/?app=desktop&platform=linux&variant=rpm"

install-manual:
    #!/bin/bash
    set -euo pipefail
    if [[ -n "{{debug}}" ]]; then
      set -x
    fi

    if [[ "${UID}" == "0" ]]; then
        SUDO=""
    else
        SUDO="sudo"
    fi

    cd rootfs

    ${SUDO} mv opt/Bitwarden usr/lib/Bitwarden
    ${SUDO} rmdir opt
    ${SUDO} mkdir usr/bin
    ${SUDO} ln -sf /usr/lib/Bitwarden/bitwarden usr/bin/bitwarden
    ${SUDO} ln -sf /usr/lib/Bitwarden/bitwarden-app usr/bin/bitwarden-app
    ${SUDO} chmod 4755 usr/lib/Bitwarden/chrome-sandbox
    ${SUDO} sed -i 's|^Exec=/opt/Bitwarden|Exec=/usr/bin|g' usr/share/applications/bitwarden.desktop
