name := "vscodium"
packages := "codium"
base_images := "
quay.io/fedora-ostree-desktops/base-atomic:41 x86_64,aarch64
quay.io/fedora-ostree-desktops/base-atomic:42 x86_64,aarch64
"

import '../sysext.just'

all: default

# Custom download step to get VSCodium RPM
download-rpms target arch=arch:
    #!/bin/bash
    set -euo pipefail
    # set -x

    mkdir rpms
    cd rpms

    arch="{{arch}}"

    echo "⬇️ Downloading VSCodium"
    url="https://api.github.com/repos/VSCodium/vscodium/releases/latest"
    curl --fail --silent "${url}" > json
    release="$(cat json | jq -r '.tag_name')"
    rpmurl="$(cat json | jq -r '.assets[] | select(.name == "codium-'${release}'-el8.'${arch}'.rpm") | .browser_download_url')"
    wget "${rpmurl}"{,.sha256}
    sha256sum -c ./*.rpm.sha256
    rm ./*.rpm.sha256 ./json
