name := "youki"
base_images := "
quay.io/fedora-ostree-desktops/base-atomic:41 x86_64,aarch64
quay.io/fedora-ostree-desktops/base-atomic:42 x86_64,aarch64
"

import '../sysext.just'

all: default

# Custom download step to get the binary
download-manual arch=arch:
    #!/bin/bash
    set -euo pipefail
    if [[ -n "{{debug}}" ]]; then
      set -x
    fi

    mkdir binaries
    cd binaries

    arch="{{arch}}"
    version="0.5.3" # TODO
    if [[ "${arch}" == "x86_64" ]]; then
        sha256sum="173b8998cd0abf22e38e36611b34cc19a16431b353dd893e3d988cfc77b4e6ac"
    else
        sha256sum="a15dfe9a1eec2d595b9a972a8a0fa1a919ee3d3523e77ca8c22099bfadf7e88d"
    fi

    echo "⬇️ Downloading youki"
    curl --location --fail --output "youki-${version}-${arch}-gnu.tar.gz" \
        "https://github.com/youki-dev/youki/releases/download/v${version}/youki-${version}-${arch}-gnu.tar.gz"
    echo "${sha256sum}  youki-${version}-${arch}-gnu.tar.gz" | sha256sum --check

    # Version & metadata
    echo "${version}" > ../version
    echo "${sha256sum} youki-${version}-${arch}-gnu.tar.gz" > ../inputs

    tar xf youki-${version}-${arch}-gnu.tar.gz
    rm youki-${version}-${arch}-gnu.tar.gz README.md

# Skip default recipe
version:
    true

# Custom installation step to install the binary
install-manual:
    #!/bin/bash
    set -euo pipefail
    if [[ -n "{{debug}}" ]]; then
      set -x
    fi

    if [[ "${UID}" == "0" ]]; then
        SUDO=""
    else
        SUDO="sudo"
    fi

    cd rootfs

    echo "➡️ Installing youki"
    ${SUDO} install -D -m 755 ../binaries/youki   usr/bin/youki
    ${SUDO} install -D -m 644 ../binaries/LICENSE usr/share/licenses/youki/LICENSE

    ${SUDO} chown -R root: usr
