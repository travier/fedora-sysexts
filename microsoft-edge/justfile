name := "microsoft-edge"
packages := "microsoft-edge-stable"
base_images := "
quay.io/fedora-ostree-desktops/base-atomic:41 x86_64
quay.io/fedora-ostree-desktops/base-atomic:42 x86_64
"
pre_commands := "rm -rf /opt; mkdir /opt; rpm --import https://packages.microsoft.com/keys/microsoft.asc; echo -e '[edge-yum]\nname=edge-yum\nbaseurl=https://packages.microsoft.com/yumrepos/edge/\nenabled=1\ngpgcheck=1\ngpgkey=https://packages.microsoft.com/keys/microsoft.asc' > /etc/yum.repos.d/edge.repo"

import '../sysext.just'

all: default

install-manual:
    #!/bin/bash
    set -euo pipefail
    if [[ -n "{{debug}}" ]]; then
      set -x
    fi

    if [[ "${UID}" == "0" ]]; then
        SUDO=""
    else
        SUDO="sudo"
    fi

    cd rootfs

    ${SUDO} mv opt/microsoft/msedge usr/lib/microsoft-edge
    ${SUDO} rmdir opt/{microsoft,}
    ${SUDO} ln -sf /usr/lib/microsoft-edge/microsoft-edge usr/bin/microsoft-edge-stable
    ${SUDO} mkdir -p usr/share/icons/hicolor/{16x16/apps,24x24/apps,32x32/apps,48x48/apps,64x64/apps,128x128/apps,256x256/apps}
    for i in "16" "24" "32" "48" "64" "128" "256"; do
        ${SUDO} ln -sf /usr/lib/microsoft-edge/product_logo_$i.png usr/share/icons/hicolor/${i}x${i}/apps/microsoft-edge.png
    done
    ${SUDO} rm -rf etc
