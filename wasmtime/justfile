name := "wasmtime"
base_images := "
quay.io/fedora-ostree-desktops/base-atomic:41 x86_64,aarch64
quay.io/fedora-ostree-desktops/base-atomic:42 x86_64,aarch64
"

import '../sysext.just'

all: default

# Custom download step to get wasmtime
download-manual arch=arch:
    #!/bin/bash
    set -euo pipefail
    if [[ -n "{{debug}}" ]]; then
      set -x
    fi

    mkdir binaries
    cd binaries

    # TODO
    version="31.0.0"
    arch="{{arch}}"
    if [[ "${arch}" == "x86_64" ]]; then
        sha256sum="ba5f2aebd96d9d81bdb98e18ba6b98b9519c27256d37b1fce283dae00eef543e"
    else
        sha256sum="efcef82b5f4d3a0c78bf14cd8827cef3d3be191c7ff05c5f6949386ff23802cf"
    fi

    echo "⬇️ Downloading wasmtime"
    destfile="wasmtime-v${version}-${arch}-linux.tar.xz"
    wget "https://github.com/bytecodealliance/wasmtime/releases/download/v${version}/${destfile}"
    echo "${sha256sum}  ${destfile}" | sha256sum -c -
    tar xf "${destfile}"
    mv wasmtime-*/{wasmtime,LICENSE} .

    # Version & metadata
    echo "${version}" > ../version
    echo "${sha256sum}  ${destfile}" > ../inputs

# Skip default recipe
version:
    true

# Custom installation step to install bandwhich
install-manual:
    #!/bin/bash
    set -euo pipefail
    if [[ -n "{{debug}}" ]]; then
      set -x
    fi

    if [[ "${UID}" == "0" ]]; then
        SUDO=""
    else
        SUDO="sudo"
    fi

    cd rootfs

    echo "➡️ Installing bandwhich"
    ${SUDO} install -D -m 755 ../binaries/wasmtime usr/bin/wasmtime
    ${SUDO} install -D -m 644 ../binaries/LICENSE usr/share/licenses/wasmtime/LICENSE

    ${SUDO} chown -R root: usr
