name := "tools"
packages := "
btop
bwm-ng
distrobox
git-absorb
git-delta
htop
igt-gpu-tools
iotop
just
ripgrep
rocm-smi
strace
wireguard-tools
zoxide
zsh
"
base_images := "
quay.io/fedora/fedora-coreos:next
quay.io/fedora-ostree-desktops/kinoite:41
"

import '../sysext.just'

default target: (check target) download-binary (download-rpms target) setup-rootfs install-binary install-rpms install-files rm-ignored (reset-selinux-labels target) build-erofs

download-binary:
    #!/bin/bash
    set -euo pipefail
    # set -x

    echo "üßπ Cleaning up previous binaries"
    rm -rf ./binaries
    mkdir binaries
    cd binaries

    echo "‚¨áÔ∏è Downloading bandwhich"
    destfile="bandwhich-v0.23.0-x86_64-unknown-linux-gnu.tar.gz"
    wget "https://github.com/imsnif/bandwhich/releases/download/v0.23.0/${destfile}"
    hash="be4ed3a35efa76e78f6ab92c708c9672145b4129f459f6950e48c0458cf727d7  ${destfile}"
    echo "${hash}" | sha256sum -c -
    tar xf "${destfile}"
    gzip -9 assets/bandwhich.1

install-binary:
    #!/bin/bash
    set -euo pipefail
    # set -x

    if [[ "${UID}" == "0" ]]; then
        SUDO=""
    else
        SUDO="sudo"
    fi

    cd rootfs

    echo "‚û°Ô∏è Installing bandwhich"
    ${SUDO} install -D -m 755 ../binaries/bandwhich             usr/bin/bandwhich
    ${SUDO} install -D -m 644 ../binaries/assets/bandwhich.bash usr/share/bash-completion/completions/bandwhich.bash
    ${SUDO} install -D -m 644 ../binaries/assets/_bandwhich     usr/share/zsh/site-functions/_bandwhich
    ${SUDO} install -D -m 644 ../binaries/assets/bandwhich.fish usr/share/fish/vendor_completions.d/bandwhich.fish
    ${SUDO} install -D -m 644 ../binaries/assets/bandwhich.1.gz usr/share/man/man1/bandwhich.1.gz

    ${SUDO} chown -R root: usr
